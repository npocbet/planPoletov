
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	//получаем идентификатор настройки
	ИдНастройки =
	КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьИдентификаторПоОбъекту
	(КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра
	(Новый ПараметрКомпоновкиДанных("Дата")));
	
	//получаем параметр по идентификатору
	ПараметрСКДДата =
	КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдНастройки).Значение.Дата;
	
	//в случае если параметр отчета (дата) отличается от предыдущей настройки - проверяем состояние отправки
	Если ПараметрСКДДата <> ДатаДокумента Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтправкаНаПочту.Период КАК Период
		|ИЗ
		|	РегистрСведений.ОтправкаНаПочту КАК ОтправкаНаПочту
		|ГДЕ
		|	ОтправкаНаПочту.Период = &Период
		|	И ОтправкаНаПочту.ТипОтправляемогоДокумента = &ТипОтправляемогоДокумента";
	
		Запрос.УстановитьПараметр("Период", ПараметрСКДДата);
		Запрос.УстановитьПараметр("ТипОтправляемогоДокумента", Перечисления.ТипОтправляемогоДокумента.ОтчетДиспетчераСОП);
		
		Если Запрос.Выполнить().Пустой() Тогда
			Отправлено = "Отчет еще не отправлялся";
		Иначе 
			Отправлено = "Отчет отправлен ранее";			
		КонецЕсли;
		
		ДатаДокумента = ПараметрСКДДата;
		
	КонецЕсли;
	                         	
	// настройки масштаба и ориентации 
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб = Истина;
	
КонецПроцедуры
